bh:=hround(baseh#*hppp); bw:=hround(basew#*hppp);
font_normal_space:=basew#;
font_normal_stretch:=.5basew#;
font_normal_shrink:=.33basew#;
font_x_height:=baseh#;
font_quad:=basew#;
font_extra_space:=basew#;
beginchar("a",basew#,baseh#,0); "kleine letters...";
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h);
draw(.2w,.5h)..(.45w,0)..(.8w,.5h)..{left}(.55w,h)..(.2w,.5h);
draw(.9w,h){down}..(.8w,.5h)..(.9w,0)..{dir20}(w,.25h);
endchar;
beginchar("b",basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.2w,bh);
draw(.2w,bh){dir45}..{left}(.8w,h)..(.2w,.3h)..(.45w,0)..(.5w,.3h);
draw(.5w,.3h)..{dir20}(w,.3h);
endchar;
beginchar("c",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h);
draw(.8w,.8h)..{left}(.55w,h)..(.2w,.5h)..(.45w,0)..{dir20}(w,.25h);
endchar;
beginchar("d",basew#,baseh#+.8upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..(.2w,.5bh);
draw(.2w,.5bh)..(.45w,0)..(.8w,.5bh)..(.55w,.bh)..(.2w,.5bh);
draw(.9w,h){down}..(.8w,.5bh)..(.9w,0)..{dir20}(w,.25bh);
endchar;
beginchar("e",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h){right}..{up}(.8w,.85h)..
{left}(.55w,h)..{down}(.2w,.5h)..{right}(.45w,0)..{dir20}(w,.25h);
endchar;
beginchar("f",.7basew#,baseh#+upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.2bw,bh);
draw(.2bw,bh){dir45}..{left}(w,h)..(.2bw,.3h)..
(.1bw,0)..{right}(.2bw,-d)..(0,-.1d)..{dir20}(w,.25bh);
endchar;
beginchar("g",basew#,baseh#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h);
draw(.2w,.5h)..(.45w,0)..(.8w,.5h)..{left}(.55w,h)..(.2w,.5h);
draw(.8w,h){down}..(.8w,.5h)..{left}(.5w,-d)..{dir20}(w,.25h);
endchar;
beginchar("h",basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.2w,bh);
draw(.2w,bh){dir45}..{left}(.8w,h)..{down}(.1w,0);
draw(.1w,0){up}..{right}(.55w,bh)..{right}(.8w,0)..{dir20}(w,.25bh);
endchar;
beginchar("i",.5basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.5w,bh);
draw(.5w,bh){down}..{right}(.7w,0)..{dir20}(w,.25bh); drawdot(.6w,h);
endchar;
beginchar("j",.7basew#,baseh#+.6upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.7w,bh);
draw(.7w,bh){down}..{left}(.3w,-d)..{dir20}(w,.25bh); drawdot(.8w,h);
endchar;
beginchar("k",basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.2w,bh);
draw(.2w,bh){dir45}..{left}(.8w,h)..{down}(.1w,0);
draw(.2w,.7bh){dir45}..{right}(.45w,bh)..{down}(.8w,.7bh)..{left}(.2w,.5bh);
draw(.2w,.5bh){dir-45}..{right}(.8w,0)..{dir20}(w,.25bh);
endchar;
beginchar("l",.7basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.2bw,bh);
draw(.2bw,bh){dir45}..{left}(w,h)..{down}(.2bw,bh)..
{right}(.45bw,0)..{dir20}(w,.25bh);
endchar;
beginchar("m",1.5basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.2w,h)..{down}(.25w,0);
draw(.25w,0){up}..{right}(.4w,h)..{down}(.5w,0);
draw(.5w,0)..{right}(.7w,h)..{right}(.8w,0)..{dir20}(w,.25h);
endchar;
beginchar("n",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.3w,h)..{down}(.4w,0);
draw(.4w,0){up}..{right}(.7w,h)..{right}(.8w,0)..{dir20}(w,.25h);
endchar;
beginchar("o",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h);
draw(.2w,.5h)..{right}(.45w,0)..{up}(.8w,.5h)..{left}(.55w,h)..{down}(.2w,.5h);
draw(.35w,.8h){dir-135}..{dir20}(w,.7h);
endchar;
beginchar("p",basew#,baseh#,.7dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.3w,h);
draw(.3w,h)..(.05w,-d);
draw(.25w,.5h)..{right}(.55w,h)..{right}(.75w,0)..{dir20}(w,.25h);
endchar;
beginchar("q",basew#,baseh#,.7dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..(.2w,.5h);
draw(.2w,.5h)..(.45w,0)..(.8w,.5h)..{left}(.55w,h)..(.2w,.5h);
draw(.8w,h)..(.6w,-d); draw(.8w,.2h)..{dir20}(w,.25h);
endchar;
beginchar("r",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{dir135}(.2w,h);
draw(.2w,h){dir-30}..(.8w,h);
draw(.8w,h){dir-135}..{right}(.8w,0)..{dir20}(w,.25h);
endchar;
beginchar("s",.8basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{dir45}(.5w,h);
draw(.5w,h){dir-70}..{down}(.8w,.2h)..{left}(.6w,0)..{right}(.6w,.2h);
draw(.8w,.2h)..{dir20}(w,.25h);
endchar;
beginchar("t",.8basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..(.3w,.65h)..(.4w,h);
draw(.4w,h){down}..(.3w,.65h)..{right}(.7w,0)..{dir20}(w,.25bh);
draw(.3w,.65h)..(w,.7h);
endchar;
beginchar("u",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{up}(.2w,h);
draw(.2w,h){down}..{right}(.45w,0)..{up}(.7w,h);
draw(.7w,h){down}..{right}(.85w,0)..{dir20}(w,.25h);
endchar;
beginchar("v",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.15w,h)..{right}(.55w,0)..{dir135}(.75w,.9h);
draw(.75w,.9h)..(w,.7h);
endchar;
beginchar("w",1.5basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.15w,h)..{right}(.3w,0)..{up}(.5w,.8h);
draw(.5w,.8h){down}..{right}(.65w,0)..{dir125}(.85w,.95h);
draw(.85w,.95h)..(w,h);
endchar;
beginchar("x",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.2w,h)..{down}(.5w,.5h)..{left}(.2w,0);
draw(.85w,.95h)..{left}(.8w,h)..{down}(.5w,.5h)..{right}(.9w,0)..
{dir20}(w,.25h);
endchar;
beginchar("y",basew#,baseh#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{right}(.15w,h)..{right}(.55w,0)..{up}(.85w,h);
draw(.85w,h){down}..{left}(.5w,-d)..{dir20}(w,.25h);
endchar;
beginchar("z",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25h){dir20}..{up}(.25w,h);
draw(.25w,h){dir-45}..(.85w,.95h);
draw(.85w,.95h){dir-135}..(.25w,0);
draw(.25w,0){dir30}..{right}(.7w,0)..{dir20}(w,.25h);
endchar;
beginchar("A",1.3basew#,baseh#+upph#,0); "hoofdletters...";
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.1h){down}..{right}(.1w,0)..{dir60}(.4w,.4h)..
(.7w,.9h)..{right}(.8w,h)..(.8w,.9h)..{down}(.8w,0){up}..
{left}(.5w,.5h)..{down}(.4w,.4h)..{right}(.5w,.3h)..{dir20}(w,.25bh);
endchar;
beginchar("B",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.2w,.9h){down}..{left}(.1w,0)..{up}(0,.5h)..
{right}(.5w,h)..{left}(.4w,.5h){right}..{left}(.7w,0)..
{up}(.5w,.1h)..{dir20}(w,.25bh);
endchar;
beginchar("C",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.5w,.9h){right}..{up}(.8w,.95h)..{left}(.5w,h)..
{down}(0,.5h)..{right}(.5w,0)..{dir20}(w,.25bh);
endchar;
beginchar("D",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.6w,.9h){dir-135}..{left}(.05w,0)..{up}(0,.05h)..
{right}(.05w,.1h)..{right}(.5w,0)..{up}(.9w,.5h)..
{left}(.7w,h)..{down}(.1w,.7h)..{dir20}(w,.25bh);
endchar;
beginchar("E",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h){dir-20}..{up}(.8w,.9h)..{left}(.5w,h)..
{down}(.1w,.8h)..{right}(.6w,.4h){left}..{down}(0,.2h)..
{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("F",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.8w,h);
draw(.8w,h){down}..(.7w,.2h)..{left}(.4w,0)..(0,.1h);
draw(.6w,.5h)..(w,.5h); draw(.8w,.3h)..{dir20}(w,.25bh);
endchar;
beginchar("G",1.3basew#,baseh#+upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.1h)..{dir60}(.4w,.4h)..{up}(.9w,.9h)..{left}(.8w,h)..
{down}(.4w,.4h)..{right}(.5w,0)..{up}(.9w,.4h){down}..
{left}(.5w,-d)..{dir20}(w,.25bh);
endchar;
beginchar("H",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.4w,h);
draw(.4w,h){down}..{down}(.3w,.2h)..{left}(.1w,0)..
{up}(0,.1h)..{up}(w,.9h)..{left}(.8w,h)..{down}(.5w,.8h)..
{down}(.4w,.2h)..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("I",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.9w,h);
draw(.9w,h){down}..(.8w,.2h)..{left}(.3w,0)..(0,.1h);
draw(.8w,.2h)..{dir20}(w,.25bh);
endchar;
beginchar("J",1.3basew#,baseh#+upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.9w,h);
draw(.9w,h){down}..(.8w,.2h)..{left}(.3w,-d)..{dir20}(w,.25bh);
endchar;
beginchar("K",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.4w,h);
draw(.4w,h){down}..{down}(.3w,.2h)..{left}(.1w,0)..(0,.1h);
draw(w,h){left}..{left}(.3w,.4h){right}..{down}(.6w,.2h)..
(.9w,0)..{dir20}(w,.25bh);
endchar;
beginchar("L",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.7w,.9h){right}..{up}(.9w,.95h)..{left}(.7w,h)..
{dir-135}(.6w,.9h)..{left}(.1w,0)..{up}(0,.05h)..
{right}(.1w,.1h)..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("M",1.5basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.1h){down}..{right}(.1w,0)..(.4w,.8h)..{right}(.5w,h)..
{dir-100}(.55w,.9h)..(.45w,.2h)..{right}(.5w,.1h)..(.6w,.2h)..
(.8w,.9h)..{right}(.9w,h)..{down}(w,.9h)..{down}(.8w,.1h)..
{right}(.9w,0)..{dir20}(w,.25bh);
endchar;
beginchar("N",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.1h){down}..{right}(.1w,0)..{dir60}(.6w,h){down}..
{down}(.6w,0){dir60}..(.85w,.95h)..{right}(.95w,h)..(w,.9h);
draw(.7w,.2h)..{dir20}(w,.25bh);
endchar;
beginchar("O",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.3w,.9h){dir-135}..{down}(0,.3h)..{right}(.4w,0)..
{up}(w,.5h)..{left}(.6w,h)..{down}(.1w,.6h)..{dir20}(w,.25bh);
endchar;
beginchar("P",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.35w,.9h){down}..{left}(.1w,0)..{up}(0,.5h)..
{right}(.5w,h)..{down}(w,.75h)..{left}(.4w,.5h);
draw(.3w,.1h)..{dir20}(w,.25bh);
endchar;
beginchar("Q",1.3basew#,baseh#+upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.75w,.2h)..{left}(.2w,0)..{up}(0,.5h)..{right}(.6w,h)..
{down}(w,.8h)..{left}(.1w,-d)..{up}(0,-.9d)..{right}(.1w,-.8d)..
{right}(.7w,-d)..(w,-.9d);
draw(.85w,.1h)..{dir20}(w,.25bh);
endchar;
beginchar("R",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.2w,.9h){down}..{left}(.1w,0)..{up}(0,.5h)..{right}(.5w,h)..
{left}(.4w,.5h){right}..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("S",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.7h){right}..{up}(.9w,.9h)..{left}(.6w,h)..{down}(.35w,.7h)..
{down}(.9w,.2h)..{left}(.4w,0)..(0,.2h); draw(.9w,.2h)..{dir20}(w,.25bh);
endchar;
beginchar("T",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.9w,h);
draw(.9w,h){dir-135}..{down}(.1w,.3h)..{right}(.5w,0)..{dir20}(w,.25bh);
endchar;
beginchar("U",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.7h){dir45}..{right}(.1w,h)..{down}(.2w,.8h)..{down}(0,.1h)..
{right}(.2w,0)..{up}(w,h){down}..(.8w,.2h)..{right}(.9w,0)..{dir20}(w,.25bh);
endchar;
beginchar("V",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.4w,h);
draw(.4w,h)..(.3w,.1h)..{right}(.35w,0)..(.45w,.1h)..(.8w,.9h)..
{right}(.9w,h)..(w,.9h); draw(.5w,.1h)..{dir20}(w,.25bh);
endchar;
beginchar("W",1.5basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.3w,h);
draw(.3w,h)..(.25w,.1h)..{right}(.3w,0)..(.35w,.1h)..
{up}(.5w,.5h){down}..{right}(.7w,0)..(.8w,.1h)..{right}(.85w,h)..(w,.9h);
draw(.8w,.1h)..{dir20}(w,.25bh);
endchar;
beginchar("X",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.8h)..{right}(.4w,h)..{down}(.5w,.5h)..{left}(.2w,0)..(0,.1h);
draw(.3w,.5h)..(.7w,.5h);
draw(w,.8h)..{left}(.8w,h)..{down}(.5w,.5h)..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("Y",1.3basew#,baseh#+upph#,dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.7h){dir45}..{right}(.1w,h)..{down}(.2w,.8h)..{down}(0,.1h)..
{right}(.2w,0)..{up}(.9w,h){down}..{left}(.3w,-d)..{dir20}(w,.25bh);
endchar;
beginchar("Z",1.3basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.9h)..(.1w,h);
draw(.1w,h){dir-30}..(.9w,h);
draw(.9w,h){dir-135}..{left}(.1w,0)..{up}(0,.05h)..{right}(.1w,.1h)..
{right}(.8w,0)..{dir20}(w,.25bh); draw(.4w,.5h)..(.9w,.5h);
endchar;
beginchar("0",basew#,baseh#+.6upph#,0); "cijfers...";
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.1w,.3h){down}..{right}(.3w,0)..{up}(.9w,.7h)..{left}(.7w,h)..
{down}(.1w,.3h);
endchar;
beginchar("1",.7basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.1w,.6h)..{up}(.9w,h); draw(.9w,h)..(.4w,0); draw(.1w,0)..(.7w,0);
endchar;
beginchar("2",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.7w,.8h)..{up}(.4w,.9h)..{right}(.6w,h)..{down}(.8w,.8h)..
{left}(.2w,0)..{up}(.1w,.05h)..{right}(.2w,.1h)..{right}(.8w,0)..(.9w,.1h);
endchar;
beginchar("3",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.4w,.95h)..{right}(.6w,h)..{down}(.8w,.85h)..{left}(.4w,.6h){right}..
{down}(.9w,.2h)..{left}(.7w,0)..(.1w,.2h);
endchar;
beginchar("4",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.6w,h)..(.1w,.5h); draw(.1w,.5h)..(.9w,.4h); draw(.8w,.7h)..(.7w,0);
endchar;
beginchar("5",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.4w,.9h)..(.2w,.6h);
draw(.2w,.6h){right}..{down}(.9w,.3h)..{left}(.7w,0)..(.1w,.2h);
draw(.2w,.9h)..(.9w,h);
endchar;
beginchar("6",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.9w,h)..{down}(.1w,.2h)..{right}(.5w,0)..{up}(.8w,.2h)..
{left}(.5w,.5h)..(.1w,.1h);
endchar;
beginchar("7",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.1w,h){dir-20}..(.8w,h); draw(.8w,h)..(.2w,0); draw(.1w,.5h)..(.9w,.5h);
endchar;
beginchar("8",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.2w,.8h){down}..{down}(.9w,.3h)..{left}(.4w,0)..{up}(.1w,.2h)..
{up}(.9w,.8h)..{left}(.6w,h)..{down}(.2w,.8h);
endchar;
beginchar("9",basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.9w,.8h){down}..{left}(.5w,.6h)..{up}(.3w,.8h)..{right}(.7w,h)..
{down}(.9w,.8h)..(.8w,.2h)..{left}(.5w,0)..(.1w,.1h);
endchar;
beginchar("!",.5basew#,baseh#+upph#,0); "leestekens...";
pickup pencircle scaled xsc; drawdot(.5w,0); draw(.5w,h)..(.5w,.2h);
endchar;
beginchar("&",basew#,baseh#+.7upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.8w,.7h){down}..{left}(.4w,0)..{up}(.2w,.2h)..{up}(.6w,.8h)..
{left}(.5w,h)..{down}(.2w,.8h)..{dir-45}(.8w,0);
endchar;
beginchar("'",.5basew#,baseh#+.5upph#,0);
pickup pencircle scaled xsc; drawdot(.6w,h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.75w,h){down}..(.4w,bh);
endchar;
ligtable "'": "'"=:oct"175";
beginchar(oct"175",.7basew#,baseh#+.5upph#,0);
pickup pencircle scaled xsc; drawdot(.3w,h); drawdot(.7w,h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.4w,h){down}..(.2w,bh); draw(.8w,h){down}..(.6w,bh);
endchar;
beginchar("(",.7basew#,baseh#+.7upph#,.5dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.7w,h){dir-135}..{down}(.3w,.4h)..{dir-45}(.7w,-d);
endchar;
beginchar(")",.7basew#,baseh#+.7upph#,.5dowh#);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.3w,h){dir-45}..{down}(.7w,.4h)..{dir-135}(.3w,-d);
endchar;
beginchar(",",.5basew#,0,.3dowh#);
pickup pencircle scaled xsc; drawdot(.5w,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.5w,0){down}..(.2w,-d);
endchar;
beginchar("-",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.2w,.7h)..(.8w,.7h);
endchar;
ligtable "-": "-"=:oct"173";
beginchar(oct"173",basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.7h)..(w,.7h);
endchar;
ligtable oct"173": "-"=:oct"174";
beginchar(oct"174",1.5basew#,baseh#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.7h)..(w,.7h);
endchar;
beginchar(".",.5basew#,0,0);
pickup pencircle scaled xsc; drawdot(.5w,0);
endchar;
beginchar(":",.5basew#,baseh#,0);
pickup pencircle scaled xsc; drawdot(.5w,0); drawdot(.5w,.9h);
endchar;
beginchar(";",.5basew#,baseh#,.3dowh#);
pickup pencircle scaled xsc; drawdot(.5w,0); drawdot(.5w,.9h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.5w,0){down}..(.2w,-d);
endchar;
beginchar("?",.8basew#,baseh#+upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
drawdot(.5w,0);
draw(.3w,.7h){up}..{right}(.5w,h)..{down}(.8w,.7h)..{down}(.5w,.2h);
endchar;
beginchar("`",.5basew#,baseh#+.5upph#,0);
pickup pencircle scaled xsc; drawdot(.5w,h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.3w,h){down}..(.7w,bh);
endchar;
ligtable "`": "`"=:oct"134";
beginchar(oct"134",.7basew#,.1baseh#,.4dowh#);
pickup pencircle scaled xsc; drawdot(.3w,.1h); drawdot(.7w,.1h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(.2w,h){down}..(.4w,-d); draw(.6w,h){down}..(.8w,-d);
endchar;
beginchar(oct"042",0,baseh#+.6upph#,0); "trema";
pickup pencircle scaled xsc; drawdot(.3bw,h); drawdot(.7bw,h);
endchar;
ligtable oct"042": "i"=:oct"014";
beginchar(oct"014",.5basew#,baseh#+.6upph#,0);
pickup pencircle scaled xsc; drawdot(.2w,h); drawdot(.9w,h);
pickup pencircle xscaled xsc yscaled ysc rotated 30;
draw(0,.25bh){dir20}..{up}(.5w,bh);
draw(.5w,bh){down}..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
beginchar("/",0,baseh#+.6upph#,0); "accent";
pickup pencircle xscaled xsc yscaled ysc rotated 30; draw(.7bw,h)..(.3bw,.8h);
endchar;
ligtable "/": "i"=:oct"016";
beginchar(oct"016",.5basew#,baseh#+.6upph#,0);
pickup pencircle xscaled xsc yscaled ysc rotated 30; draw(.9w,h)..(.2w,.8h);
draw(0,.25bh){dir20}..{up}(.5w,bh);
draw(.5w,bh){down}..{right}(.7w,0)..{dir20}(w,.25bh);
endchar;
